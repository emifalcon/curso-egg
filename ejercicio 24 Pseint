//Disponemos de un vector unidimensional de 20 elementos de tipo carácter. Se pide
//desarrollar un programa que:
//	a) Pida una frase al usuario y luego ingrese la frase dentro del arreglo letra por letra.
//Ayuda: utilizar la función Subcadena de PSeInt.
//	b) Una vez completado lo anterior, pedirle al usuario un carácter cualquiera y una
//	posición dentro del arreglo, y el programa debe intentar ingresar el carácter en la
//	posición indicada, si es que hay lugar (es decir la posición está vacía o es un espacio
//	en blanco). De ser posible debe mostrar el vector con la frase y el carácter ingresado,
//	de lo contrario debe darle un mensaje al usuario de que esa posición estaba ocupada. 


Algoritmo Guia4Ej6
	Definir vectoruni, vectoruni2 Como Caracter;
	Dimension vectoruni[20], vectoruni2[20];
	rellenarLetra(vectoruni);
	copy(vectoruni, vectoruni2);
	reemplazo(vectoruni);
	print(vectoruni2);
	print(vectoruni);
FinAlgoritmo

SubProceso copy(vectoruni, vectoruni2)
	Definir i Como Entero;
	Para i = 0 Hasta 19 Hacer
		vectoruni2[i]=vectoruni[i]
	FinPara
FinSubProceso

SubProceso rellenarLetra(vectoruni)
	Definir frase Como Caracter;
	Definir i Como Entero;
	Escribir "Ingrese la frase";
	Leer frase;
	Para i = 0 hasta 19 Hacer
		vectoruni[i]=Subcadena(frase,i,i);
	FinPara
FinSubProceso

SubProceso print(vectoruni)
	Definir i Como Entero;
	Para i = 0 Hasta 19 Hacer
		Escribir Sin Saltar vectoruni[i];
	FinPara
	Escribir "";
FinSubProceso

SubProceso reemplazo (vectoruni)
	Definir car Como Caracter;
	Definir i, lugar Como Entero;
	Escribir "Ingrese el caracter";
	Leer car;
	Escribir "Ingrese la posicion del 0 al 19";
	Leer lugar;
	Si lugar<20 y lugar>=0 Entonces
		Si vectoruni[lugar] = "" o vectoruni[lugar] = " " Entonces
			vectoruni[lugar] = car;
		SiNo
			Escribir "Lugar ocupado";
		FinSi
	SiNo
		Escribir "El lugar ingresado esta fuera de los paramentro";
	FinSi
	Escribir "";
FinSubProceso
	
